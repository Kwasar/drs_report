!Electro-thermo-mechanical simulation using coupled-field elements
FINISH
/CLEAR
/TITLE,Electro-thermo-mechanical simulation
/COM ---- Parameters
l1=200e-6
l2=600e-6
l3=15e-6
w1=10E-6	!see class notes for the geometry
w2=10e-6
w3=50e-6
t=5e-6		!Thickness of beam
tgap=5e-6	!Thickness of the gap
Tsub=300	!Temperature of substrate
V=40

!Silicon
CTE=2.6e-6	!coefficient of thermal expansion for Silicon (1/K)
Y=169e9		!Young Modulus of Si (We consider istotropic properties)
nu=0.36		!Poisson coefficient
rho=9E-4	!Resistivity (Ohm*m) of Boron-doped silicon for 1E19 at/cm^3
kth=149		!thermal conductivity (W/m/K)

!Air
Y_air=1e-3	!to avoid mesh collapse problems
nu_air=0.1	
kth_air=0.024	!Thermal Conductivity
rho_air=1E9		!very large electrical resistance for air

/COM --- Geometry and elements
/PREP7
ET,1,SOLID226,111	!3-D 20-Node Coupled-Field Solid. Switch on DOF disp,thermal, and electric conduction


!Material Properties for the Silicon beam
MP,EX,1,Y		!Apply Young's modulus
MP,PRXY,1,nu	!Apply Poisson ratio
MP,RSVX,1,rho		!Apply resistivity
MPTEMP,1,300,500,700,900,1100,1300
MPTEMP,7,1500
MPDATA,ALPX,1,1,2.568e-6,3.594e-6,3.987e-6,4.185e-6,4.323e-6,4.442e-6
MPDATA,ALPX,1,7,4.556e-6
MPDATA,KXX,1,1,146.4,73.2,49.2,37.6,31.4,27.2
MPDATA,KXX,1,7,25.1

!Material Properties for the air gap
MP,EX,2,Y_air
MP,PRXY,2,nu_air
MP,KXX,2,kth_air
MP,RSVX,2,rho_air

TREF,Tsub		!The reference temperature used for dilatation calculations (alpha*(T-Tref)) is the substrate temperature

!The geometry is split into rectangles so that Mapped meshing can be used.
K,01,0,0			!Define keypoints 
K,03,l1+l2,0
K,04,l1+l2+l3,0

K,11,0,w1
K,13,l1+l2,w1
K,14,l1+l2+l3,w1

K,21,0,w1+w2
K,22,l1,w1+w2
K,23,l1+l2,w1+w2
K,24,l1+l2+l3,w1+w2

K,31,0,2*w1+w2
K,32,l1,2*w1+w2
K,33,l1+l2,2*w1+w2
K,34,l1+l2+l3,2*w1+w2

K,42,l1,2*w1+w2+w3
K,43,l1+l2,2*w1+w2+w3
K,44,l1+l2+l3,2*w1+w2+w3

!surfaces
A,01,03,13,11
A,03,04,14,13
A,13,14,24,23
A,21,22,32,31
A,22,23,33,32
A,23,24,34,33
A,32,33,43,42	
A,33,34,44,43

!Creates the volume by extrusion in the thicknedd direction
*DO,k,1,8,1
VOFFST,k,t !Creates the volume by extrusion in the thicknedd direction
VOFFST,k,-tgap
*ENDDO

NUMMRG,KP,1e-9,1e-9	!merge the coincident keypoints that were created with the VOFFST command

ESIZE,t

VSEL,S,LOC,Z,t/2
VATT,1	!Material 1 (silicon)
VMESH,ALL

VSEL,S,LOC,Z,-tgap/2
VATT,2	!Material 1 (silicon)
VMESH,ALL

ALLSEL,ALL

/COM --- Loads and Boundary conditions

ASEL,S,LOC,X,0		!Select the areas a X=0
DA,ALL,UX,0			!Lock all displacement on those areas (anchor)
DA,ALL,UY,0
DA,ALL,UZ,0
DA,ALL,TEMP,Tsub
DA,ALL,VOLT,0
ASEL,R,LOC,Y,w1/2
DA,ALL,VOLT,V
ASEL,S,LOC,Z,-tgap
DA,ALL,TEMP,Tsub
DA,ALL,VOLT,0
ALLSEL,ALL			!Reselects all
FINISH


/SOLU
ANTYPE,STATIC
CNVTOL,f,1,1.e-4     ! Define convergence tolerances
CNVTOL,heat,1,1.e-5
CNVTOL,amps,1,1.e-5
!nlgeom,on            ! Large deflection analysis
NSUBST,5,20,2
SOLVE
FINISH

/POST1
/CONTOUR,ALL,128
PLNSOL, U,Y, 2,1.0	!plot the displacement along y

PATH,P1,4,,100
PPATH,1,,0,w1/2,t
PPATH,2,,l1+l2+l3/2,w1/2,t
PPATH,3,,l1+l2+l3/2,1.5*w1+w2,t
PPATH,4,,0,1.5*w1+w2,t

PDEF,,TEMP,,AVG
PDEF,,VOLT,,AVG
PDEF,,JC,SUM,AVG